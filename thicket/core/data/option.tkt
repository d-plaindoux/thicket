/*
 * Thicket
 * https://github.com/d-plaindoux/thicket
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Data.Option

from Data.Boolean import bool,true,false
from Data.Try import try,failure,success
from Data.String import string
from Data.Monadic import Monadic
from Data.Unit import unit

from Logic.Combinators import combinators
 
type Option[a] {
    Some { _ : a }
    None
}

class option[a] this:Option[a] {
    with Monadic[option a]
    
    unbox     : Option[a]
    fold      : [b] b -> (a -> b) -> b
    present   : bool
    ifPresent : (a -> unit) -> unit
    orElse    : a -> a
} {
    def unbox = this
    
    def None.fold n _ = n
    def Some.fold _ s = s this._

    def map f = self fold none (s -> some $ f s)
    def flatmap f = self fold none f

    def None.filter _ = self
    def Some.filter p = p this._ fold self $ option None

    def present = self fold false (_ -> true)    
    def ifPresent = self fold ()
    def orElse a = self fold a combinators.I
} 

/**
 * Factories
 */

def some : [a] a -> option[a] = e -> { option $ Some e }
def none : [a] option[a] = { option None }

/**
 * Adapters
 */

def adapter option2try : [a] option[a] -> try[a] = t -> {
    t fold (failure "Undefined value") success
}