/*
 * Thicket
 * https://github.com/d-plaindoux/thicket
 *
 * Copyright (c) 2015-2016 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Data.Sequence

from Data.Number import number
from Data.Boolean import bool, false, true
from Data.Option import option, some , none
from Data.Comparable import Comparable
from Lang.When import when

//
// Models definition
//

model Sequence[a] {
    length      : number
    get         : number -> option[a]
    isDefinedAt : number -> bool
    indexOf     : Comparable[a] -> option[number]
    lastIndexOf : Comparable[a] -> option[number]
}

model Modifiable[a,t] {
    set : number -> a -> t
}

//
// Traits definition
//

trait sequence[a] {
    with Sequence[a]
} {
    def isDefinedAt i = 
        self get i fold false (_ -> true)
        
    def _indexOf_ : Comparable[a] -> number -> (number -> bool) -> (number -> number) -> option[number] = a i last next ->
        when (last i)
        then none
        else $ self get i flatmap 
             $ b -> when (a==b)
                    then { some i }
                    else { self _indexOf_ a (next i) last next }             
        
    def indexOf a = 
        self _indexOf_ a 0 (self.length==) (i -> i++)

    def lastIndexOf a = 
        self _indexOf_ a (self.length--) (0==) (i -> i--)
}

trait modifiable[a,t] {
    with Modifiable[a,t]
}
