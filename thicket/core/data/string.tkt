/*
 * Thicket
 * https://github.com/d-plaindoux/thicket
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Data.String

from System.Internal import internal

from Data.Boolean import bool,true,false
from Data.Native import native
from Data.Number import number
from Data.Char import char
from Data.Option import option,none,some
from Data.Comparable import comparable
from Data.Hashable import hashable
from Data.Matcher import Matcher

from Lang.Switch import switch

class string this:native {
    with comparable[string]
    with hashable[string]
    with Matcher[string]
        
    getAt    : number -> option[char]
    setAt    : number -> char -> string

    (+)      : string -> string   
    (*)      : number -> string    
    
    length   : number

    toNumber : option[number] 
} {
    def unbox = self

    def (==) n = internal "generic.==" self n true false
    def (<?) n = internal "generic.<" self n true false
    
    def hash = number $ internal "string.hash" self

    def match = switch self

    def getAt n = internal "string.getAt" self n (n -> some $ char n) none
    def setAt n v = string $ internal "string.setAt" self n v
    
    def (+) s = string $ internal "string.+" self s
    def (*) n = 1 to n fold "" (r _ -> self + r)
    
    def length = number $ internal "string.length" self    
    
    def toNumber = internal "string.toNumber" self (n -> some $ number n) none
}

/**
 * Adapters
 */

def adapter string2number : string -> option[number] = string::toNumber
