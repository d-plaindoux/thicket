/*
 * Thicket
 * https://github.com/d-plaindoux/thicket
 *
 * Copyright (c) 2015 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Examples.Nat
 
from Data.Number import number
from Data.String import string
 
type Nat {
    model Zero
    model Succ { _ : Nat }
}
 
class nat this:Nat {
    unbox : Nat
    fold  : [a] a -> (nat -> a) -> a
    (+)   : nat -> nat
} {
    def unbox = this
    
    def Zero.fold z s = z
    def Succ.fold z s = s (nat this._)
    
    def Zero.(+) p = p
    def Succ.(+) p = nat $ Succ $ nat this._ + p unbox
}
 
class natInterp this:nat {
    toNumber : number
    toString : string
} {
    def toNumber = 
        let onSucc = p -> 1 + $ natInterp p toNumber in
            this fold 0 onSucc
            
    def toString = 
        let onSucc = p -> "Succ(" + (natInterp p toString) + ")" in
            this fold "Zero" onSucc
}

def succ : nat -> nat = n -> nat $ Succ n.unbox
def zero : nat = nat Zero
def one  : nat = succ zero
